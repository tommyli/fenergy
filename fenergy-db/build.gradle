buildscript {
  ext {
    postgresDriverVersion = '42.2.5'
  }
  dependencies {
    classpath "org.postgresql:postgresql:$postgresDriverVersion"
  }
}

plugins {
  id 'org.springframework.boot' apply false
}
apply plugin: 'io.spring.dependency-management'
dependencyManagement {
  imports {
    mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
  }
}

apply plugin: 'org.flywaydb.flyway'

description = "Fenergy DB"

dependencies {
  runtimeOnly("org.flywaydb:flyway-core")
}

flyway {
  driver = project.property('fenergy.db.ddl.driver')
  schemas = [project.property('fenergy.db.ddl.schema')]
  url = project.property('fenergy.db.ddl.url')
  user = project.property('fenergy.db.ddl.userid')
  password = project.property('fenergy.db.ddl.password')
  locations = ["classpath:db/migration"]
  placeholderReplacement = true
  placeholders = [
    'schemaName': project.property('fenergy.db.ddl.schema'),
    'dbUserId'  : project.property('fenergy.db.ddl.userid')
  ]
}

flywayClean.dependsOn classes
flywayMigrate.dependsOn classes
flywayMigrate.dependsOn flywayClean
