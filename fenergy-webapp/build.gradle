//import co.firefire.fenergy.db.RunSqlScriptTask

buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    classpath("com.google.cloud.tools:appengine-gradle-plugin:+")
    classpath("com.moowork.gradle:gradle-node-plugin:+")
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:+"
  }
}

plugins {
  id 'org.springframework.boot' version '1.5.9.RELEASE'
}
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'com.moowork.node'

description = "Fenergy Web Application Server"
group "co.firefire.fenergy.webapp"

ext {
  flywayVersion = '5.0.7'
  postgresDriverVersion = '42.1.4.jre7'
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile("org.springframework.boot:spring-boot-autoconfigure:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-cache:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-logging:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-mail:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
//  compile("org.springframework.cloud:spring-cloud-starter-zuul:$springCloudZuulVersion")
  compile("org.springframework.security.oauth:spring-security-oauth2:$springSecurityOAuth2Version")

  runtime project(":fenergy-db")
  runtime "org.flywaydb:flyway-core:$flywayVersion"
  compile project(":fenergy-nem12")

  testCompile("org.springframework.boot:spring-boot-test:$springBootVersion")
  testCompile("org.springframework:spring-test:$springTestVersion")
  testCompile("org.spockframework:spock-core:$spockVersion") {
    exclude module: 'groovy-all'
  }
  testCompile("org.spockframework:spock-spring:$spockVersion") {
    exclude module: 'groovy-all'
  }
}

def sysProps = [
  'logging.file'       : project.property('fenergy.logging.file'),
  'logging.path'       : project.property('fenergy.logging.path'),
  'fenergy.db.driver'  : project.property('fenergy.db.driver'),
  'fenergy.db.schema'  : project.property('fenergy.db.schema'),
  'fenergy.db.url'     : project.property('fenergy.db.url'),
  'fenergy.db.userid'  : project.property('fenergy.db.userid'),
  'fenergy.db.password': project.property('fenergy.db.password')
]

bootRun {
  jvmArgs = ['-Dspring.output.ansi.enabled=always']
  systemProperties = sysProps
}

[bootRun, test].each { task ->
  configure(task) {
    systemProperties = sysProps
  }
}

appengine {
  deploy {
    stopPreviousVersion = true
    promote = true
  }
}

test {
  testLogging {
    events 'passed', 'skipped', 'failed', 'standardError'
    minGranularity 2
    maxGranularity 2
  }
}

node {
  version = "6.9.1"
  npmVersion = "5.3.0"
  download = false
  nodeModulesDir = file("${project(':fenergy-ui').projectDir}")
}

task clientStart(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = 'Build client for development'
  args = ['run', 'start']
}

task clientBuild(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = 'Build client for development'
  args = ['run', 'build']
}

task clientTest(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = "Run client tests"
  args = ['run', 'test']
}

task clientBuildProd(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = 'Build client for production'
  args = ['run', 'buildProd']
}

bootRun.dependsOn(clientBuild)
//jar.dependsOn(clientBuildProd)
