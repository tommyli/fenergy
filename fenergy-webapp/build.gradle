description = "Fenergy Web Application Server"

buildscript {
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:+")
    classpath("com.google.cloud.tools:appengine-gradle-plugin:+")
    classpath("com.moowork.gradle:gradle-node-plugin:+")
  }
}

group "co.firefire.fenergy"

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

ext {
  nodeVersion = '6.9.1'
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile("org.springframework.boot:spring-boot-autoconfigure")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-cache")
  compile("org.springframework.boot:spring-boot-starter-logging")
  compile("org.springframework.boot:spring-boot-starter-mail")
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-tomcat")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.security.oauth:spring-security-oauth2")

  testCompile("org.springframework.boot:spring-boot-test")
  testCompile("org.springframework:spring-test")
  testCompile("org.spockframework:spock-core:$spockVersion")
  testCompile("org.spockframework:spock-spring:$spockVersion")
}

def sysProps = [
  'logging.file': project.property('fenergy.logging.file'),
  'logging.path': project.property('fenergy.logging.path'),
]

springBoot {
  executable = true
}

bootRun {
  jvmArgs = ['-Dspring.output.ansi.enabled=always']
  systemProperties = sysProps
}

[bootRun, test].each { task ->
  configure(task) {
    systemProperties = sysProps
  }
}

node {
  version = "$nodeVersion"
  download = true
  nodeModulesDir = file("${project(':fenergy-ui').projectDir}")
}

appengine {
  deploy {
    stopPreviousVersion = true
    promote = true
  }
}

test {
  testLogging {
    events 'passed', 'skipped', 'failed', 'standardError'
    minGranularity 2
    maxGranularity 2
  }
}

task clientServe(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = "Build and watches the client side assets for rebuilding"
  args = ['run', 'start']
}

task clientBuild(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = 'Build client for development'
  args = ['run', 'build']
}

task clientTest(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = "Run client tests"
  args = ['run', 'test']
}

task clientBuildProd(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'Angular CLI Client Tasks'
  description = 'Build client for production'
  args = ['run', 'buildProd']
}

bootRun.dependsOn(clientBuild)
jar.dependsOn(clientBuildProd)
