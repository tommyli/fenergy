// Tommy Li (tommy.li@firefire.co), 2017-01-16

import co.firefire.n12m.db.RunSqlScriptTask

buildscript {
  repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath gradleApi()
    classpath "org.postgresql:postgresql:42.1.1.jre7"
  }
}

plugins {
  id "org.flywaydb.flyway" version "4.1.0"
}

version "0.1.0"
group "co.firefire.n12m.db"

apply plugin: "idea"
apply plugin: "org.grails.grails-core"

repositories {
  mavenLocal()
  maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
  imports {
    mavenBom "org.grails:grails-bom:$grailsVersion"
  }
  applyMavenExclusions false
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-logging"
  compile "org.springframework.boot:spring-boot-autoconfigure"
  compile "org.grails:grails-core"
  console "org.grails:grails-console"
  profile "org.grails.profiles:base"

  compile "org.postgresql:postgresql:42.1.1.jre7"
}

flyway {
  driver = project.property('n12m.db.driver')
  schemas = [project.property('n12m.db.schema')]
  url = project.property('n12m.ci.db.url')
  user = project.property('n12m.ci.db.userid')
  password = project.property('n12m.ci.db.password')
  locations = ["filesystem:${project.projectDir}/src/main/sql/migrations/"]
  placeholderReplacement = true
  placeholders = [
    'schemaName': project.property('n12m.db.schema'),
    'dbUserId': project.property('n12m.ci.db.userid')
  ]
  skipDefaultResolvers = false
  skipDefaultCallbacks = false
  outOfOrder = false
}

task flywayMigrateProd(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  schemas = [project.property('n12m.db.schema')]
  url = project.property('n12m.prod.db.url')
  user = project.property('n12m.prod.db.userid')
  password = project.property('n12m.prod.db.password')
  placeholders = [
    'schemaName': project.property('n12m.db.schema'),
    'dbUserId': project.property('n12m.prod.db.userid')
  ]
}

task setupTestData(type: RunSqlScriptTask) {
  url = project.property('n12m.ci.db.url')
  userid = project.property('n12m.ci.db.userid')
  password = project.property('n12m.ci.db.password')
  scripts = [
    'insert_test_data.sql'
  ]
}
