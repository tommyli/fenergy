// Tommy Li (tommy.li@firefire.co), 2017-01-16

import co.firefire.n12m.db.RunSqlScriptTask

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath gradleApi()
    classpath "org.postgresql:postgresql:42.1.1.jre7"
  }
}

plugins {
  id "org.flywaydb.flyway" version "4.1.0"
}

apply plugin: 'groovy'

description = "Module for mainting all DB related tasks including DB migration and setting up CI DB."
group "co.firefire.n12m.db"
defaultTasks 'setupTestData'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile("org.postgresql:postgresql:42.1.1.jre7")
}

flyway {
  driver = project.property('n12m.db.driver')
  schemas = [project.property('n12m.db.schema')]
  url = project.property('n12m.test.db.url')
  user = project.property('n12m.test.db.userid')
  password = project.property('n12m.test.db.password')
  locations = ["filesystem:${project.projectDir}/src/main/sql/migrations/"]
  placeholderReplacement = true
  placeholders = [
    'schemaName': project.property('n12m.db.schema'),
    'dbUserId': project.property('n12m.test.db.userid')
  ]
  skipDefaultResolvers = false
  skipDefaultCallbacks = false
  outOfOrder = false
}

task flywayMigrateProd(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  schemas = [project.property('n12m.db.schema')]
  url = project.property('n12m.prod.db.url')
  user = project.property('n12m.prod.db.userid')
  password = project.property('n12m.prod.db.password')
  placeholders = [
    'schemaName': project.property('n12m.db.schema'),
    'dbUserId': project.property('n12m.prod.db.userid')
  ]
}

flywayMigrate.dependsOn flywayClean

task setupTestData(type: RunSqlScriptTask, dependsOn: flywayMigrate) {
  url = project.property('n12m.test.db.url')
  userid = project.property('n12m.test.db.userid')
  password = project.property('n12m.test.db.password')
  scripts = [
    'insert_test_data.sql'
  ]
}
