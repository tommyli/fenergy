// Tommy Li (tommy.li@firefire.co), 2017-01-16

import co.firefire.n12m.db.RunSqlScriptTask

buildscript {
  ext {
    flywayVersion = '4.2.0'
  }
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:+"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    classpath "gradle.plugin.com.boxfuse.client:flyway-release:$flywayVersion"
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
  }
}

plugins {
  id 'org.springframework.boot' version '1.5.7.RELEASE'
}
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'com.google.cloud.tools.appengine'

description = "NEM12 Manager API"
group "co.firefire.n12m.api"

ext {
  flywayVersion = '4.2.0'
  postgresDriverVersion = '42.1.1.jre7'
  spockVersion = '1.1-groovy-2.4'
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile("org.springframework.boot:spring-boot-autoconfigure:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-cache:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-logging:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-groovy-templates:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-mail:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
  compile("org.springframework.cloud:spring-cloud-starter-zuul:$springCloudZuulVersion")
  compile("org.springframework.security.oauth:spring-security-oauth2:$springSecurityOAuth2Version")
  compile("org.codehaus.groovy:groovy:+")
  compile("org.jetbrains.kotlin:kotlin-stdlib:+")
  compile("org.hibernate:hibernate-entitymanager:+")
  compile("org.hibernate:hibernate-java8:+")

  compile("org.webjars:jquery:2.2.4")
  compile("org.webjars:js-cookie:2.1.3")
  compile("org.webjars:bootstrap:3.3.7")

  runtime("org.flywaydb:flyway-core:$flywayVersion")
  runtime("org.postgresql:postgresql:+")

  testCompile("org.springframework.boot:spring-boot-test:$springBootVersion")
  testCompile("org.springframework:spring-test:$springTestVersion")
  testCompile("org.spockframework:spock-core:$spockVersion")
  testCompile("org.spockframework:spock-spring:$spockVersion")
}

def sysProps = [
  'logging.file'    : project.property('n12m.logging.file'),
  'logging.path'    : project.property('n12m.logging.path'),
  'n12m.db.driver'  : project.property('n12m.db.driver'),
  'n12m.db.schema'  : project.property('n12m.db.schema'),
  'n12m.db.url'     : project.property('n12m.db.url'),
  'n12m.db.userid'  : project.property('n12m.db.userid'),
  'n12m.db.password': project.property('n12m.db.password')
]

bootRun {
  jvmArgs = ['-Dspring.output.ansi.enabled=always']
  systemProperties = sysProps
}

[bootRun, test].each { task ->
  configure(task) {
    systemProperties = sysProps
  }
}

appengine {
  deploy {
    stopPreviousVersion = true
    promote = true
  }
}

test {
  testLogging {
    events 'passed', 'skipped', 'failed', 'standardError'
    minGranularity 2
    maxGranularity 2
  }
}

flyway {
  driver = project.property('n12m.db.driver')
  schemas = [project.property('n12m.db.schema')]
  url = project.property('n12m.test.db.url')
  user = project.property('n12m.test.db.userid')
  password = project.property('n12m.test.db.password')
  locations = ["classpath:db/migration"]
  placeholderReplacement = true
  placeholders = [
    'schemaName': project.property('n12m.db.schema'),
    'dbUserId'  : project.property('n12m.test.db.userid')
  ]
}

task setupTestData(type: RunSqlScriptTask) {
  url = project.property('n12m.test.db.url')
  userid = project.property('n12m.test.db.userid')
  password = project.property('n12m.test.db.password')
  scripts = [
    'insert_test_data.sql'
  ]
}

flywayClean.dependsOn classes
flywayMigrate.dependsOn classes
flywayMigrate.dependsOn flywayClean
setupTestData.dependsOn flywayMigrate
